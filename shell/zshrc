# -*- mode:shell-script -*-
path=(
    $HOME/bin
    /usr/local/sbin
    /usr/local/bin
    /usr/sbin
    /usr/bin
    /sbin
    /bin
)

# completion
fpath=(~/.zsh/zsh-completions/src ~/.zsh/mycomp $fpath)
autoload -U compinit
compinit

# default umask
umask 022

# for move by word
autoload -Uz select-word-style
select-word-style bash

# History
HISTFILE=~/.zsh_history
HISTSIZE=50000
SAVEHIST=50000
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt share_history

# key-type is Emacs
bindkey -e

# removed keys(for miss typing)
# I want to bind good function for these keys!!
bindkey -r '^J'

bindkey '^O' list-choices

# for incremental search
bindkey '^R' history-incremental-pattern-search-backward
bindkey '^S' history-incremental-pattern-search-forward

# Disable completion command
# following command completion is too long.
compdef -d java

# not exit Ctrl-D
setopt ignoreeof

# for sighup
setopt NOHUP
setopt nocheckjobs

# stty
stty stop undef

# completion like emacs
zstyle ':completion:*:default' menu select=1

# completion use cache
zstyle ':completion:*' use-cache true

# completion after '='(like --prefix=/usr)
setopt magic_equal_subst

# with slash
setopt noautoremoveslash

# glob expand
setopt null_glob no_nomatch

# print if line is one line
unsetopt promptcr

# Not bell
setopt NO_beep
setopt nolistbeep

# String behind '#' is comment.
setopt interactive_comments

# key binding of command history
autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^P" history-beginning-search-backward-end
bindkey "^N" history-beginning-search-forward-end

# show processing time for long run process
REPORTTIME=10

# Setting for MacOSX
TEST_CONFIG=${HOME}/dot_files/emacs/test_emacs
case "$OSTYPE" in
darwin*)
        # Only Perl 5.16 problem??
        export PERLDOC_PAGER='less -f -r'

        # java setting for using iterm on mac
        alias javac='javac -J-Dfile.encoding=UTF-8'
        alias java='java -Dfile.encoding=UTF-8'
        EMACSAPP="/Applications/Emacs.app/Contents/MacOS/Emacs"
        alias test-emacs="$EMACSAPP -Q -l $TEST_CONFIG"
        ;;
linux-gnu)
        export BROWSER=firefox
        alias test-emacs="emacs -Q -l $TEST_CONFIG"
        ;;
esac

# setting LANG, this should be UTF-8
export LANG=ja_JP.UTF-8
export TERM=xterm-256color

# setup ls coloring
local LIST_COLOR='di=34;1' 'ln=35' 'so=32' 'ex=32;1' 'bd=46;34' 'cd=43;34'
zstyle ':completion:*' list-colors $LIST_COLOR
if [ "$OSTYPE" == "linux-gnu" ]; then
    alias ls='ls --color -F'
    if [ ! -e ~/.dir_colors ]; then
        dircolors -p > ~/.dir_colors
    fi
    eval `dircolors ~/.dir_colors -b`
else
    if which gls > /dev/null 2>&1; then
        alias ls='gls --color -F'
    fi
fi

# alias
alias rdesktop='rdesktop -x l -a 16 -g 90%'
alias grep='grep --color=always'
alias gg='git grep'
alias gl='git ls-files'
alias be='bundle exec'

# dstat
alias dstat-full='dstat -Tclmdrn'
alias dstat-mem='dstat -Tclm'
alias dstat-cpu='dstat -Tclr'
alias dstat-net='dstat -Tclnd'
alias dstat-disk='dstat -Tcldr'

if [ "$OSTYPE" == "linux-gnu" ] && which emacsclient.sh >/dev/null 2>&1; then
    alias e='emacsclient.sh'
else
    alias e='emacsclient'
fi

# send to clipboard for each platform
if which pbcopy >/dev/null 2>&1 ; then
   # Mac (for tmux. pbcopy and pbpaste don't work well on tmux session)
    alias -g C='| ssh 127.0.0.1 pbcopy'
elif which xsel >/dev/null 2>&1 ; then
   # Linux
   alias -g C='| xsel --input --clipboard'
fi

# PROMPT
autoload colors
colors
PROMPT="%B%{${fg[yellow]}%}%n@%m:%{${reset_color}%}%b%# "

# EDITOR
export EDITOR="emacsclient"

# PAGER setting
export PAGER='less'
export LESSCHARSET=utf-8

# info
export INFOPATH=/usr/local/share/info:/usr/share/info

# load perlbrew config file
if [ -e ~/perl5/perlbrew/etc/bashrc ]; then
    source ~/perl5/perlbrew/etc/bashrc
    unset MANPATH
fi

# load rbenv
if [ -d ~/.rbenv/bin ]; then
    path=($HOME/.rbenv/bin $path)
    eval "$(rbenv init - zsh)"
fi

# load nvm(node.js manager)
if [ -e ~/.nvm/nvm.sh ]; then
    source ~/.nvm/nvm.sh
    nvm use myuse > /dev/null
    export NODE_PATH=/home/syohei/.nvm/`node --version`/lib/node_modules:$NODE_PATH
fi

# pythonz
[[ -e ~/.pythonz/etc/bashrc ]] && source ~/.pythonz/etc/bashrc

# setting for python virtual env
VIRTUALENV_ACTIVE=~/virtual_env/bin/activate
if [ -e ${VIRTUALENV_ACTIVE} ]; then
    export VIRTUAL_ENV_DISABLE_PROMPT=1
    source ${VIRTUALENV_ACTIVE}
fi
export PYTHONSTARTUP=~/.pythonsetup

# haskell setting
[[ -d ~/.cabal/bin ]] && path=(~/.cabal/bin $path)

# Emacs Lisp Carton setting, # Don't conflict Perl's carton
[[ -d ~/.carton ]] && alias ecarton="${HOME}/.carton/bin/carton"

# loading common function for any platform
source ~/dot_files/shell/common_func.sh

## for setting cdd
source ~/dot_files/shell/cdd.zsh

## show git branch at right prompt
autoload -Uz vcs_info
zstyle ':vcs_info:*' formats '(%b)'
zstyle ':vcs_info:*' actionformats '[%s]-(%b|%a)'
precmd () {
    psvar=()
    LANG=en_US.UTF-8 vcs_info
    [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"

    if git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
        stashes=$(git stash list 2>/dev/null | wc -l)
        if [ ${stashes} -eq 0 ]; then
            psvar[2]=""
        else
            psvar[2]="@${stashes// /}"
        fi
    fi
}
_DIRPART=%B%{$fg[cyan]%}'[%(5~,%-2~/.../%2~,%~)]'%{$reset_color%}%b
RPROMPT=%B"%1(v|%F{magenta}%1v%f%F{yellow}%2v%f|)"%b${_DIRPART}

autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
zstyle ':chpwd:*' recent-dirs-max 500
zstyle ':chpwd:*' recent-dirs-default yes
zstyle ':completion:*' recent-dirs-insert both

# zaw : anything
if [ -e ~/.zsh/zaw/zaw.zsh ]; then
    source ~/.zsh/zaw/zaw.zsh
    bindkey '^[@' zaw-cdr
    bindkey '^[#' zaw-git-directories
fi

# for git completion(too slowly in big repository)
__git_files () {
    _wanted files expl 'local files' _files
}

# for tmux complete
_tmux_pane_words() {
    local expl
    local -a w
    if [[ -z "$TMUX_PANE" ]]; then
        _message "not running inside tmux!"
        return 1
    fi
    w=( ${(u)=$(tmux capture-pane \; show-buffer \; delete-buffer)} )
    _wanted values expl 'words from current tmux pane' compadd -a w
}

zle -C tmux-pane-words-prefix   complete-word _generic
zle -C tmux-pane-words-anywhere complete-word _generic
bindkey '^[/' tmux-pane-words-prefix
bindkey '^[?' tmux-pane-words-anywhere
zstyle ':completion:tmux-pane-words-(prefix|anywhere):*' completer _tmux_pane_words
zstyle ':completion:tmux-pane-words-(prefix|anywhere):*' ignore-line current
zstyle ':completion:tmux-pane-words-anywhere:*' matcher-list 'b:=*'

## create emacs env file
perl -wle \
    'do { print qq/(setenv "$_" "$ENV{$_}")/ if exists $ENV{$_} } for @ARGV' \
    PATH GEM_PATH GEM_HOME VIRTUAL_ENV \
    > ~/.emacs.d/shellenv.el

# load private data
source ~/.zsh/private.sh

## Last
# setting global alias for avoiding conflict variable name
alias -g L='|less -R'
alias -g G='|grep'
alias -g P='|ptrigger'
